






REEECR

	LDA 2,X
	LDB #40
	MUL
	TFR D,Y

	LDD ,X
	ASRA
	RORB
	ASRA
	RORB
	ASRA
	RORB

	LEAY D,Y

	LDB 1,X
	ANDB #7

	RTS





VIRECR

	LDA 2,X
	LDB #40
	MUL
	TFR D,Y
	LDD ,X
	ASRA
	RORB
	ASRA
	RORB
	ASRA
	RORB
	LEAY D,Y
	LEAY $A000,Y

	LDB 1,X
	ANDB #7


	RTS


UPDATEANIM

	LDA 4,X
	LDU #TABANIM
	JMP [A,U]


****
* DIGGER
****

ANIMDIG

	LDA 3,X
	CMPA #15
	BEQ ANIMDIG1
	RTS

ANIMDIG1

	LDU #DIGRE
	LDA 1,X
	ANDA #7
	LSLA
	LEAU A,U

	PSHS X

	JSR REEECR
	LEAY 11*40,Y

	JSR SCRCRAM
	LDD ,U
	ANDA ,Y
	STA ,Y
	ANDB 1,Y
	STB 1,Y
	LDD ,U
	ANDA -40,Y
	STA -40,Y
	ANDB -39,Y
	STB -39,Y
	LDD ,U
	ANDA $2000,Y
	STA $2000,Y
	ANDB $2001,Y
	STB $2001,Y
	LDD ,U
	ANDA $2000-40,Y
	STA $2000-40,Y
	ANDB $2000-39,Y
	STB $2000-39,Y
	JSR SCRCRAM
	LDD ,U
	ANDA ,Y
	STA ,Y
	ANDB 1,Y
	STB 1,Y
	LDD ,U
	ANDA -40,Y
	STA -40,Y
	ANDB -39,Y
	STB -39,Y
	LDD ,U
	ANDA $2000,Y
	STA $2000,Y
	ANDB $2001,Y
	STB $2001,Y
	LDD ,U
	ANDA $2000-40,Y
	STA $2000-40,Y
	ANDB $2000-39,Y
	STB $2000-39,Y

	LDA #$05
	STA $E7E5
	LDD ,U
	ANDA $C000,Y
	STA $C000,Y
	ANDB $C001,Y
	STB $C001,Y
	LDD ,U
	ANDA -40+$C000,Y
	STA -40+$C000,Y
	ANDB -39+$C000,Y
	STB -39+$C000,Y
	LDD ,U
	ANDA $A000,Y
	STA $A000,Y
	ANDB $A001,Y
	STB $A001,Y
	LDD ,U
	ANDA $A000-40,Y
	STA $A000-40,Y
	ANDB $A000-39,Y
	STB $A000-39,Y

	PULS U

	LDA #$04
	STA $E7E5


	LDU #DIGRE
	LDA 1,X
	ANDA #7
	LSLA
	LEAU A,U
	JSR VIRECR
	LEAY 11*40,Y

	LDD ,U
	ANDA ,Y
	ANDB 1,Y
	STD ,Y

	LDD ,U
	ANDA -40,Y
	ANDB -39,Y
	STD -40,Y


	INC 2,X
	INC 2,X
	JSR VIRECR
	LDA 10*40,Y
	LDU #TABMASKVER
	ANDA B,U
	BEQ ANIMDIG2


	RTS

ANIMDIG2

	LDA #2
	STA 4,X
	CLR 3,X
	DEC 2,X
	DEC 2,X

	RTS



DIGRE FDB $007F,$803F,$C01F,$E00F
	FDB $F007,$F803,$FC01,$FE00


****
* WAIT LEFT
****

ANIMWAIL

	LDA 3,X
	CMPA #11
	BEQ ANIMWAIL1
	RTS

ANIMWAIL1

	LDA #2
	STA 4,X
	CLR 3,X
	RTS


****
* BRIDGE LEFT
****

ANIMBRIL

	LDA 3,X
	CMPA #15
	BEQ ANIMBRIL1
	RTS

ANIMBRIL1


	LDU #BRIREL
	LDA 1,X
	ANDA #7
	LSLA
	LEAU A,U

	JSR REEECR
	LEAY 12*40,Y

	JSR SCRCRAM
	LDD ,U
	ORA ,Y
	ORB 1,Y
	STD ,Y
	LDD ,U
	ORA $2000,Y
	ORB $2001,Y
	STD $2000,Y

	JSR SCRCRAM
	LDD ,U
	ORA ,Y
	ORB 1,Y
	STD ,Y
	LDD ,U
	ORA $2000,Y
	ORB $2001,Y
	STD $2000,Y

	LDA #$05
	STA $E7E5
	LDD ,U
	ORA $C000,Y
	ORB $C001,Y
	STD $C000,Y
	LDD ,U
	ORA $A000,Y
	ORB $A001,Y
	STD $A000,Y

	LDA #$04
	STA $E7E5

	JSR VIRECR
	LEAY 12*40,Y

	LDD ,U
	ORA ,Y
	ORB 1,Y
	STD ,Y

	LDY ,X
	LEAY -2,Y
	STY ,X
	DEC 2,X

	DEC 7,X
	BEQ ANIMBRIL3

	LDY ,X
	LEAY -2,Y
	STY ,X

	JSR VIRECR

	LDA 12*40,Y

	LDY ,X
	LEAY 2,Y
	STY ,X

	LDU #TABMASKVER
	ANDA B,U

	BNE ANIMBRIL2
	RTS

ANIMBRIL2

	LDA #4
	STA 4,X
	CLR 3,X
	INC 2,X
	INC 2,X
	INC 2,X
	RTS

ANIMBRIL3

	LDA #24
	STA 4,X
	CLR 3,X
	INC 2,X
	INC 2,X
	INC 2,X
	INC 2,X
	RTS

BRIREL FDB $F000,$7800,$3C00,$1E00
	FDB $0F00,$0780,$03C0,$01E0



****
* WAIT RIGHT
****

ANIMWAIR

	LDA 3,X
	CMPA #11
	BEQ ANIMWAIR1
	RTS

ANIMWAIR1

	LDA #4
	STA 4,X
	CLR 3,X
	RTS


****
* BRIDGE RIGHT
****

ANIMBRIR

	LDA 3,X
	CMPA #15
	BEQ ANIMBRIR1
	RTS

ANIMBRIR1


	LDU #BRIRER
	LDA 1,X
	ANDA #7
	LSLA
	LEAU A,U

	JSR REEECR
	LEAY 12*40,Y

	JSR SCRCRAM
	LDD ,U
	ORA ,Y
	ORB 1,Y
	STD ,Y
	LDD ,U
	ORA $2000,Y
	ORB $2001,Y
	STD $2000,Y
	JSR SCRCRAM

	LDD ,U
	ORA ,Y
	STA ,Y
	ORB 1,Y
	STB 1,Y
	LDD ,U
	ORA $2000,Y
	ORB $2001,Y
	STD $2000,Y

	LDA #$05
	STA $E7E5

	LDD ,U
	ORA $C000,Y
	STA $C000,Y
	ORB $C001,Y
	STB $C001,Y
	LDD ,U
	ORA $A000,Y
	ORB $A001,Y
	STD $A000,Y


	LDA #$04
	STA $E7E5

	JSR VIRECR
	LEAY 12*40,Y

	LDD ,U
	ORA ,Y
	ORB 1,Y
	STD ,Y

	LDY ,X
	LEAY 2,Y
	STY ,X
	DEC 2,X

	DEC 7,X
	BEQ ANIMBRIR3

	LDY ,X
	LEAY 4,Y
	STY ,X

	JSR VIRECR

	LDA 12*40,Y

	LDY ,X
	LEAY -4,Y
	STY ,X

	LDU #TABMASKVER
	ANDA B,U

	BNE ANIMBRIR2
	RTS

ANIMBRIR2

	LDA #2
	STA 4,X
	CLR 3,X
	INC 2,X
	INC 2,X
	INC 2,X
	RTS

ANIMBRIR3

	LDA #20
	STA 4,X
	CLR 3,X
	INC 2,X
	INC 2,X
	INC 2,X
	INC 2,X
	RTS

BRIRER FDB $0F00,$0780,$03C0,$01E0
	FDB $00F0,$0078,$003C,$001E

****
* FLOAT LEFT
****

ANIMFLOATL

	JSR VIRECR
	LDA 16*40,Y
	LDU #TABMASKVER
	ANDA B,U
	BEQ ANIMFLOATL1

	LDA 2,X
	INCA
	INCA
	INCA
	INCA
	INCA
	INCA
	STA 2,X
	LDA #2
	STA 4,X
	RTS

ANIMFLOATL1

	INC 2,X
	RTS



****
* FLOAT RIGHT
****

ANIMFLOATR

	JSR VIRECR
	LDA 16*40,Y
	LDU #TABMASKVER
	ANDA B,U
	BEQ ANIMFLOATR1

	LDA 2,X
	INCA
	INCA
	INCA
	INCA
	INCA
	INCA
	STA 2,X
	LDA #4
	STA 4,X
	CLR 3,X


	RTS

ANIMFLOATR1

	INC 2,X
	RTS



****
* FALL RIGHT
****

ANIMFALLR

	LDA 6,X
	BEQ ANIMFALLR1
	LDA #12
	STA 4,X
	CLR 3,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X

	JMP ANIMFLOATR

ANIMFALLR1

	JSR VIRECR
	LDA 10*40,Y
	LDU #TABMASKVER
	ANDA B,U
	BEQ ANIMFALLR2

	LDA 7,X
	CMPA #50
	BGT ANIMFALLR5

	LDA #4
	STA 4,X
	CLR 3,X
	CLR 7,X
	RTS

ANIMFALLR2
	INC 2,X
	INC 7,X

	LDA 11*40,Y
	ANDA B,U
	BEQ ANIMFALLR3

	LDA 7,X
	CMPA #50
	BGT ANIMFALLR5

	LDA #4
	STA 4,X
	CLR 3,X
	CLR 7,X
	RTS

ANIMFALLR3
	INC 2,X
	INC 7,X

	LDA 12*40,Y
	ANDA B,U
	BEQ ANIMFALLR4

	LDA 7,X
	CMPA #50
	BGT ANIMFALLR5

	LDA #4
	STA 4,X
	CLR 3,X
	CLR 7,X
	RTS

ANIMFALLR4
	INC 2,X
	INC 7,X
	RTS

ANIMFALLR5

	LDA #30
	STA 4,X
	CLR 3,X
	CLR 7,X

	JMP DROPDEAD



****
* FALL LEFT
****


ANIMFALLL

	LDA 6,X
	BEQ ANIMFALLL1
	LDA #10
	STA 4,X
	CLR 3,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	JMP ANIMFLOATL

ANIMFALLL1

	JSR VIRECR
	LDA 10*40,Y
	LDU #TABMASKVER
	ANDA B,U
	BEQ ANIMFALLL2

	LDA #2
	STA 4,X
	CLR 3,X
	CLR 7,X
	RTS

ANIMFALLL2
	INC 2,X
	INC 7,X

	LDA 7,X
	CMPA #50
	BGT ANIMFALLL5


	LDA 11*40,Y
	ANDA B,U
	BEQ ANIMFALLL3

	LDA 7,X
	CMPA #50
	BGT ANIMFALLL5

	LDA #2
	STA 4,X
	CLR 3,X
	CLR 7,X
	RTS

ANIMFALLL3
	INC 2,X
	INC 7,X
	LDA 12*40,Y
	ANDA B,U
	BEQ ANIMFALLL4

	LDA 7,X
	CMPA #50
	BGT ANIMFALLL5

	LDA #2
	STA 4,X
	CLR 3,X
	CLR 7,X
	RTS

ANIMFALLL4
	INC 2,X
	INC 7,X
	RTS

ANIMFALLL5

	LDA #30
	STA 4,X
	CLR 3,X
	CLR 7,X
	JMP DROPDEAD


****
* WALK LEFT
****

ANIMWALKL

	LDD ,X
	SUBD #1
	STD ,X
	JSR VIRECR

	LDU #TABMASK16
	ASLB
	LEAU B,U

	LDD ,U
	ANDA 9*40,Y
	ANDB 9*40+1,Y
	CMPD #0
	BEQ ANIMWALKL5

	DEC 2,X
	LDD ,U
	ANDA 8*40,Y
	ANDB 8*40+1,Y
	CMPD #0
	BNE ANIMWALKL1
	RTS

ANIMWALKL1
	DEC 2,X
	LDD ,U
	ANDA 7*40,Y
	ANDB 7*40+1,Y
	CMPD #0
	BNE ANIMWALKL2
	RTS

ANIMWALKL2
	DEC 2,X
	LDD ,U
	ANDA 6*40,Y
	ANDB 6*40+1,Y
	CMPD #0
	BNE ANIMWALKL3
	RTS

ANIMWALKL3
	LDA 5,X
	BEQ ANIMWALKL4

	INC 2,X
	INC 2,X
	INC 2,X
	LDD ,X
	SUBD #1
	STD ,X
	LDA #6
	STA 4,X
	RTS

ANIMWALKL4
	INC 2,X
	INC 2,X
	INC 2,X
	LDA #4
	STA 4,X
	RTS

ANIMWALKL5
	LDD ,U
	ANDA 10*40,Y
	ANDB 10*40+1,Y
	CMPD #0
	BEQ ANIMWALKL6
	RTS

ANIMWALKL6
	INC 2,X
	LDD ,U
	ANDA 11*40,Y
	ANDB 11*40+1,Y
	CMPD #0
	BEQ ANIMWALKL7
	RTS

ANIMWALKL7
	INC 2,X
	LDD ,U
	ANDA 12*40,Y
	ANDB 12*40+1,Y
	CMPD #0
	BEQ ANIMWALKL8
	RTS

ANIMWALKL8
	INC 2,X
	LDD ,U
	ANDA 13*40,Y
	ANDB 13*40+1,Y
	CMPD #0
	BEQ ANIMWALKL9
	RTS

ANIMWALKL9
	LDA 6,X
	BEQ ANIMWALKL10

	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X

	LDA #10
	STA 4,X
	CLR 3,X
	RTS

ANIMWALKL10

	LDA #14
	STA 4,X
	CLR 3,X
	RTS


****
* WALK RIGHT
****

ANIMWALKR

	LDD ,X
	ADDD #1
	STD ,X
	JSR VIRECR

	LDU #TABMASK16
	ASLB
	LEAU B,U
	LDD ,U
	ANDA 9*40,Y
	ANDB 9*40+1,Y
	CMPD #0
	BEQ ANIMWALKR5

	DEC 2,X
	LDD ,U
	ANDA 8*40,Y
	ANDB 8*40+1,Y
	CMPD #0
	BNE ANIMWALKR1
	RTS

ANIMWALKR1
	DEC 2,X
	LDD ,U
	ANDA 7*40,Y
	ANDB 7*40+1,Y
	CMPD #0
	BNE ANIMWALKR2
	RTS

ANIMWALKR2
	DEC 2,X
	LDD ,U
	ANDA 6*40,Y
	ANDB 6*40+1,Y
	CMPD #0
	BNE ANIMWALKR3
	RTS

ANIMWALKR3
	LDA 5,X
	BEQ ANIMWALKR4

	LDD ,X
	SUBD #3
	STD ,X
	INC 2,X
	INC 2,X
	INC 2,X
	LDA #8
	STA 4,X
	RTS

ANIMWALKR4
	INC 2,X
	INC 2,X
	INC 2,X
	LDA #2
	STA 4,X
	RTS

ANIMWALKR5
	LDD ,U
	ANDA 10*40,Y
	ANDB 10*40+1,Y
	CMPD #0
	BEQ ANIMWALKR6
	RTS

ANIMWALKR6
	INC 2,X
	LDD ,U
	ANDA 11*40,Y
	ANDB 11*40+1,Y
	CMPD #0
	BEQ ANIMWALKR7
	RTS

ANIMWALKR7
	INC 2,X
	LDD ,U
	ANDA 12*40,Y
	ANDB 12*40+1,Y
	CMPD #0
	BEQ ANIMWALKR8
	RTS

ANIMWALKR8
	INC 2,X
	LDD ,U
	ANDA 13*40,Y
	ANDB 13*40+1,Y
	CMPD #0
	BEQ ANIMWALKR9
	RTS

ANIMWALKR9
	LDA 6,X
	BEQ ANIMWALKR10

	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X

	LDA #12
	STA 4,X
	CLR 3,X
	RTS

ANIMWALKR10

	LDA #16
	STA 4,X
	CLR 3,X
	RTS


GOINGIN


	RTS


TOGOOUT

	LDA 7,X
	BNE TOGOOUT1
	RTS

TOGOOUT1

	INCA
	INC 7,X
	LDY #LEVCNT
	CMPA ,Y
	BHS TOGOOUT2

	RTS

TOGOOUT2

	LDA #16
	STA 4,X
	CLR 3,X
	CLR 7,X

	LDY #TOGO
	INC ,Y
TOGOOUT3

	LDA #10
	CMPA ,Y
	BEQ TOGOOUT4

	INC 20+7,X


TOGOOUT4

	RTS

***
* STOPPER
***


STOPPER

	JSR VIRECR
	ASLB
	LDU #TABMASK16
	LEAU B,U
	LDD ,U
	ANDA 10*40,Y
	ANDB 10*40+1,Y
	CMPD #0
	BNE STOPPER1

	CLR 3,X
	LDA #4
	STA 4,X


STOPPER1

	RTS


BOMBER

	LDA 3,X
	CMPA #15
	BEQ BOMBER1
	RTS

BOMBER1

	CLR 3,X
	CLR 4,X
	DEC NBLEM


	JSR REEECR

	LDU #BOMBMASK
	LDA 3,U
	STA $E7E5
	LDU ,U

	LDB 1,X
	ANDB #7
	CMPB #3
	BGT BOMBER2
	LEAY -1,Y

BOMBER2

	LEAY -80,Y
	LDA #69
	MUL

	LEAU D,U

	JSR BOMBDISMASK
	JSR SCRCRAM
	JSR BOMBDISMASK
	LEAY $2000,Y
	JSR BOMBDISMASK
	JSR SCRCRAM
	JSR BOMBDISMASK

	PSHS X
	LDB #69
	LDX #BOMBBANK

BOMBER3
	LDA ,U+
	STA ,X+
	DECB
	BNE BOMBER3

	PULS X

	LDU #BOMBBANK
	LDA #5
	STA $E7E5
	LEAY $8000,Y
	JSR BOMBDISMASK
	LEAY $2000,Y
	JSR BOMBDISMASK


	LDU #BOMBBANK
	LDA #4
	STA $E7E5

	JSR VIRECR
	CMPB #3
	BGT BOMBER4
	LEAY -1,Y
BOMBER4
	LEAY -80,Y
	JSR BOMBDISMASK


	RTS


BOMBBANK RMB 69




BOMBDISMASK

	PSHS Y,U
	LDB #23

BOMBDISMASK1

	LDA ,Y
	ANDA ,U+
	STA ,Y
	LDA 1,Y
	ANDA ,U+
	STA 1,Y
	LDA 2,Y
	ANDA ,U+
	STA 2,Y

	LEAY 40,Y

	DECB
	BNE BOMBDISMASK1


	PULS Y,U

	RTS


DIGBANK	RMB 20

DIGRIGHT

	LDA 3,X
	CMPA #2
	BEQ DIGRIGHT1

	RTS

DIGRIGHT1

	LDY #DIGMASK
	LDA 2,Y
	STA $E7E5
	LDU ,Y
	LDB 1,X
	ANDB #7
	LDA #20
	MUL
	LEAU D,U

	LDY #DIGBANK

	LDA #20

DIGRIGHT2

	LDB ,U+
	STB ,Y+
	DECA
	BNE DIGRIGHT2


	JSR REEECR
	LEAY 1,Y
	LDA 1,X
	ANDA #7
	CMPA #3
	BGT DIGRIGHT3
	LEAY -1,Y

DIGRIGHT3

	LDU #DIGBANK
	LEAY 120,Y

	JSR SCRCRAM
	JSR DIGDISMASK
	JSR SCRCRAM
	JSR DIGDISMASK
	LEAY $2000,Y
	JSR SCRCRAM
	JSR DIGDISMASK
	JSR SCRCRAM
	JSR DIGDISMASK

	LDA #5
	STA $E7E5
	LEAY $8000,Y
	JSR DIGDISMASK
	LEAY $2000,Y
	JSR DIGDISMASK

	LDA #4
	STA $E7E5
	LEAY -$2000,Y
	JSR DIGDISMASK

	INC 2,X
	LDD ,X
	ADDD #2
	STD ,X

	LDA 10*40,Y
	LDU #TABMASKVER
	LDB 1,X
	ANDB #7
	ANDA B,U
	BEQ DIGRIGHT4
	RTS

DIGRIGHT4


	LDA #4
	STA 4,X
	CLR 3,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	RTS



DIGLEFT

	LDA 3,X
	CMPA #2
	BEQ DIGLEFT1

	RTS

DIGLEFT1

	LDY #DIGMASK
	LDA 5,Y
	STA $E7E5
	LDU 3,Y
	LDB 1,X
	ANDB #7
	LDA #20
	MUL
	LEAU D,U

	LDY #DIGBANK

	LDA #20

DIGLEFT2

	LDB ,U+
	STB ,Y+
	DECA
	BNE DIGLEFT2


	JSR REEECR
	LEAY -1,Y
	LDA 1,X
	ANDA #7
	CMPA #4
	BLT DIGLEFT3
	LEAY 1,Y

DIGLEFT3

	LDU #DIGBANK
	LEAY 120,Y

	JSR SCRCRAM
	JSR DIGDISMASK
	JSR SCRCRAM
	JSR DIGDISMASK
	LEAY $2000,Y
	JSR SCRCRAM
	JSR DIGDISMASK
	JSR SCRCRAM
	JSR DIGDISMASK

	LDA #5
	STA $E7E5
	LEAY $8000,Y
	JSR DIGDISMASK
	LEAY $2000,Y
	JSR DIGDISMASK

	LDA #4
	STA $E7E5
	LEAY -$2000,Y
	JSR DIGDISMASK

	INC 2,X
	LDD ,X
	SUBD #2
	STD ,X

	LDA 10*40,Y
	LDU #TABMASKVER
	LDB 1,X
	ANDB #7
	ANDA B,U
	BEQ DIGLEFT4
	RTS

DIGLEFT4


	LDA #2
	STA 4,X
	CLR 3,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	RTS


DIGDISMASK

	LDA #10
	PSHS U,Y

DIGDISMASK1

	PSHS A
	LDD ,Y
	ANDA ,U
	ANDB 1,U
	STD ,Y

	LEAY 40,Y
	LEAU 2,U

	PULS A

	DECA
	BNE DIGDISMASK1

	PULS U,Y

	RTS


ANIMCLIMBL

	LDA 3,X
	CMPA #5
	BLT ANIMCLIMBL1

	DEC 2,X

ANIMCLIMBL1

	JSR VIRECR
	ASLB
	LDU #TABMASKLH
	LEAU B,U

	LDD ,U
	ANDA ,Y
	ANDB 1,Y
	CMPD #0
	BEQ ANIMCLIMBL2

	LDB 1,X
	ANDB #7
	ASLB
	LDU #TABMASKLV
	LEAU B,U
	LDD ,U
	ANDA ,Y
	ANDB 1,Y
	CMPD #0
	BEQ ANIMCLIMBL3


	LDD ,X
	ADDD #2
	STD ,X
	CLR 3,X
	LDA #16
	STA 4,X
	RTS


ANIMCLIMBL2



	CLR 3,X
	LDA #2
	STA 4,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X

ANIMCLIMBL3


	RTS



ANIMCLIMBR

	LDA 3,X
	CMPA #5
	BLT ANIMCLIMBR1

	DEC 2,X

ANIMCLIMBR1

	LDD ,X
	ADDD #3
	STD ,X

	JSR VIRECR
	LDU #TABMASKRH
	LDD ,X
	ADDD #7
	ANDB #7
	ASLB
	LEAU B,U

	LDD ,U
	ANDA ,Y
	ANDB 1,Y
	CMPD #0
	BEQ ANIMCLIMBR2

	LDB 1,X
	ANDB #7
	ASLB
	LDU #TABMASKRV
	LEAU B,U
	LDD ,U
	ANDA ,Y
	ANDB 1,Y
	CMPD #0
	BEQ ANIMCLIMBR3

	LDD ,X
	SUBD #3
	STD ,X
	CLR 3,X
	LDA #14
	STA 4,X
	RTS


ANIMCLIMBR2

	LDD ,X
	ADDD #4
	STD ,X
	CLR 3,X
	LDA #4
	STA 4,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	DEC 2,X

ANIMCLIMBR3

	LDD ,X
	SUBD #3
	STD ,X

	RTS

BASHRIGHT


	LDA 3,X
	CMPA #3
	BEQ BASRIGHT1
	CMPA #19
	BEQ BASRIGHT1

	RTS

BASRIGHT1

	LDD ,X
	ADDD #6
	STD ,X
	JSR VIRECR
	LDU #TABMASKVER
	LDA 40*8,Y
	ANDA B,U
	LBEQ BASRIGHT4

	LDD ,X
	SUBD #6
	STD ,X


	LDY #DIGMASK
	LDA 2,Y
	STA $E7E5
	LDU ,Y
	LDB 1,X
	ANDB #7
	LDA #20
	MUL
	LEAU D,U

	LDY #DIGBANK

	LDA #20

BASRIGHT2

	LDB ,U+
	STB ,Y+
	DECA
	BNE BASRIGHT2


	JSR REEECR
	LEAY 1,Y
	LDA 1,X
	ANDA #7
	CMPA #3
	BGT BASRIGHT3
	LEAY -1,Y

BASRIGHT3

	LDU #DIGBANK
	LEAY -40,Y

	JSR SCRCRAM
	JSR DIGDISMASK
	JSR SCRCRAM
	JSR DIGDISMASK
	LEAY $2000,Y
	JSR SCRCRAM
	JSR DIGDISMASK
	JSR SCRCRAM
	JSR DIGDISMASK

	LDA #5
	STA $E7E5
	LEAY $8000,Y
	JSR DIGDISMASK
	LEAY $2000,Y
	JSR DIGDISMASK

	LDA #4
	STA $E7E5
	LEAY -$2000,Y
	JSR DIGDISMASK

	LDD ,X
	ADDD #2
	STD ,X

	RTS

BASRIGHT4

	LDD ,X
	SUBD #6
	STD ,X
	LDA #4
	STA 4,X
	CLR 3,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	RTS

	RTS



BASHLEFT


	LDA 3,X
	CMPA #3
	BEQ BASLEFT1
	CMPA #19
	BEQ BASLEFT1

	RTS

BASLEFT1

	LDD ,X
	SUBD #2
	STD ,X
	JSR VIRECR
	LDU #TABMASKVER
	LDA 40*8,Y
	ANDA B,U
	LBEQ BASLEFT4

	LDD ,X
	ADDD #2
	STD ,X


	LDY #DIGMASK
	LDA 5,Y
	STA $E7E5
	LDU 3,Y
	LDB 1,X
	ANDB #7
	LDA #20
	MUL
	LEAU D,U

	LDY #DIGBANK

	LDA #20

BASLEFT2

	LDB ,U+
	STB ,Y+
	DECA
	BNE BASLEFT2

	JSR REEECR
	LDA 1,X
	ANDA #7
	CMPA #3
	BGT BASLEFT3
	LEAY -1,Y

BASLEFT3

	LDU #DIGBANK
	LEAY -40,Y

	JSR SCRCRAM
	JSR DIGDISMASK
	JSR SCRCRAM
	JSR DIGDISMASK
	LEAY $2000,Y
	JSR SCRCRAM
	JSR DIGDISMASK
	JSR SCRCRAM
	JSR DIGDISMASK

	LDA #5
	STA $E7E5
	LEAY $8000,Y
	JSR DIGDISMASK
	LEAY $2000,Y
	JSR DIGDISMASK

	LDA #4
	STA $E7E5
	LEAY -$2000,Y
	JSR DIGDISMASK

	LDD ,X
	SUBD #2
	STD ,X

	RTS

BASLEFT4

	LDD ,X
	ADDD #2
	STD ,X
	LDA #2
	STA 4,X
	CLR 3,X
	DEC 2,X
	DEC 2,X
	DEC 2,X
	RTS

	RTS





DROPDEAD
NOTHING
	RTS


CURPOS FCB 0


TABMASKVER

	FCB 64,32,16,8,4,2,1,1

TABMASK16

	FDB $F000,$7800,$3C00,$1E00
	FDB $0F00,$0780,$03C0,$01E0

TABMASKRV

	FDB $E000,$7000,$3800,$1C00
	FDB $0E00,$0700,$0380,$01C0

TABMASKRH

	FDB $0F00,$0780,$03C0,$01E0
	FDB $00F0,$0078,$003C,$001E


TABMASKLH

	FDB $C000,$6000,$3000,$1800
	FDB $0C00,$0600,$0300,$0180

TABMASKLV

	FDB $3000,$1800,$0C00,$0600
	FDB $0300,$0180,$00C0,$0060


TABANIM
	FDB NOTHING,ANIMWALKL
	FDB ANIMWALKR,ANIMCLIMBL
	FDB ANIMCLIMBR,ANIMFLOATL
	FDB ANIMFLOATR,ANIMFALLL
	FDB ANIMFALLR,ANIMBRIR
	FDB ANIMWAIR,ANIMBRIL
	FDB ANIMWAIL,ANIMDIG
	FDB STOPPER,DROPDEAD
	FDB GOINGIN,TOGOOUT
	FDB BOMBER,DIGRIGHT
	FDB DIGLEFT,BASHRIGHT
	FDB BASHLEFT
